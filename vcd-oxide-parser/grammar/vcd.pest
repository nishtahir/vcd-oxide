WHITESPACE = _{ " " | "\t" | NEWLINE }
file = _ {SOI ~ value_change_dump_definitions ~ EOI }
value_change_dump_definitions = { (declaration_command | simulation_command )* }
declaration_command = { declaration_keyword ~ command_text* ~ end }
command_text = { (!(end) ~ ANY)+ }
comment_text = { (!(end) ~ ANY)+ }
declaration_keyword = { 
	"$comment" 
    | "$date" 
    | "$enddefinitions" 
    | "$scope" 
    | "$timescale" 
    | "$upscope"
    | "$var" 
    | "$version" 
}
simulation_command = { 
	simulation_keyword_command
    | simulation_keyword_comment
    | simulation_time
    | value_change
}

simulation_keyword_command = {
	simulation_keyword ~ value_change+ ~ end
}

simulation_keyword = {
    "$dumpall" 
    | "$dumpoff" 
    | "$dumpon" 
    | "$dumpvars"
}

simulation_keyword_comment = {
    ("$comment" ~ comment_text ~ end)
}

simulation_time  = { "#" ~ decimal_number }
decimal_number = { 
	unsigned_number
	| ("'" ~ ("s" | "S")? ~ ("d" | "D")) 
}
unsigned_number = @{ ('0'..'9') ~ (('0'..'9') | "_")* }
value_change = { scalar_value_change | vector_value_change }
scalar_value_change = { value ~ identifier_code }
vector_value_change = { 
	(binary_value ~ identifier_code) | (real_value ~ identifier_code)
}
real_value = { ("r" | "R") ~ unsigned_value }
unsigned_value = @{ digit ~ ("_" | digit) }
binary_value = @{ ("b" | "B") ~ value ~ ( "_" | value )* }
value = { "0" | "1" | "x" | "X" | "z" | "Z" }
digit = { '0'..'9' }
identifier_code = @{ identifier_char+ }
identifier_char =  { !(WHITESPACE) ~ ASCII }
end = _{ "$end" }